apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'

android {
    namespace = "com.github.barteksc.pdfviewer"
    compileSdk 34
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "2.8.2"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

}

dependencies {
    implementation("androidx.core:core-ktx:1.9.0")

    // PDF engine
    api 'com.github.VERO-Digital-Solutions:android-support-pdfium:1.0.1'

    // OpenPDF
    implementation 'com.github.librepdf:openpdf:1.3.23'
    implementation 'com.github.andob:android-awt:1.0.1'

    // For deserializing shape classes, converting them to JSON string
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    // For moving the IO operations to background thread
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.7.1'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                setGroupId 'com.github.VERO-Digital-Solutions'
                setArtifactId 'AndroidPdfViewerV2'
                version '1.0.2'
                artifact androidSourcesJar
                artifact bundleReleaseAar
                // Add dependencies to the POM file
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def pdfiumDependency = dependenciesNode.appendNode('dependency')
                    pdfiumDependency.appendNode('groupId', 'com.github.VERO-Digital-Solutions')
                    pdfiumDependency.appendNode('artifactId', 'android-support-pdfium')
                    pdfiumDependency.appendNode('version', '1.0.1')

                    def openPdfDependency = dependenciesNode.appendNode('dependency')
                    openPdfDependency.appendNode('groupId', 'com.github.librepdf')
                    openPdfDependency.appendNode('artifactId', 'openpdf')
                    openPdfDependency.appendNode('version', '1.3.23')

                    def androidAwtDependency = dependenciesNode.appendNode('dependency')
                    androidAwtDependency.appendNode('groupId', 'com.github.andob')
                    androidAwtDependency.appendNode('artifactId', 'android-awt')
                    androidAwtDependency.appendNode('version', '1.0.0')
                }
            }
        }
    }
}